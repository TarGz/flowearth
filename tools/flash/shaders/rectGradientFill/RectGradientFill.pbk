<languageVersion : 1.0;>

kernel RectGradientFill
<   namespace : "fr.digitas.flowearth.shaders::RectGradientFill";
    vendor : "Pierre Lepers";
    version : 1;
    description : "Create a rectGradient fill, suited for vector dropShadow...";
>
{
    
    parameter float4 insetColor
    < 
        description : "inset gradient color";
        defaultValue:float4( 0.0, 0.0, 0.0, 1.0 ); 
    >;
    
    parameter float4 outsetColor
    < 
        description : "outset gradient color";
        defaultValue:float4( 0.0, 0.0, 0.0, 0.0 ); 
    >;
    
    parameter float2 size
    < 
        description : "[w, h] size of the base rect zone of the gradient";
        defaultValue:float2(100.0, 100.0); 
    >;
    
    
    parameter float2 dist
    < 
        description : "[x,y] gradient size for each x, y coordinates";
        defaultValue:float2(12.0, 12.0); 
    >;
    
    output pixel4 dst;
    
    #define ZERO float2( 0.0, 0.0 )

    void
    evaluatePixel()
    {
        float2 rectCenter = float2( size.x/2.0, size.y/2.0 );
        
       
        float2 distances = max( ZERO, abs( rectCenter - outCoord() ) - rectCenter );
        
        
        
        float rdist = clamp( length( distances / dist ), 0.0, 1.0 );
        
        rdist = -rdist *( rdist - 2.0 );
        
        dst = mix( insetColor, outsetColor, rdist );
        
        // float2 rdist = ( float2( distance( rectCenter.x, outCoord().x ), distance( rectCenter.y, outCoord().y ) ) - misize ) / dist ;
        //dst = mix( mix( insetColor, outsetColor, rdist.x ), mix( insetColor, outsetColor, rdist.y ), .5);
    }
}
