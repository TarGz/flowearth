<languageVersion : 1.0;>

kernel RectGradientFill
<   namespace : "fr.digitas.flowearth.shaders::RectGradientFill";
    vendor : "Pierre Lepers";
    version : 1;
    description : "Create a rectGradient fill, suited for vector dropShadow...";
>
{
    
    parameter float4 insetColor
    < 
        description : "inset gradient color";
        defaultValue:float4( 0.0, 0.0, 0.0, 1.0 ); 
    >;
    
    parameter float4 outsetColor
    < 
        description : "outset gradient color";
        defaultValue:float4( 0.0, 0.0, 0.0, 0.0 ); 
    >;
    
    parameter float4 rect
    < 
        description : "[x, y, w, h] the base rect zone of the gradient";
        defaultValue:float4(12.0, 12.0, 100.0, 100.0); 
    >;
    
    
    parameter float2 dist
    < 
        description : "[x,y] gradient size for each x, y coordinates";
        defaultValue:float2(12.0, 12.0); 
    >;
    
    output pixel4 dst;
    
    #define ZERO float2( 0.0, 0.0 )

    void
    evaluatePixel()
    {
        float2 misize = float2( rect.z/2.0, rect.w/2.0 );
        
        float2 rectCenter = float2( rect.x + misize.x, rect.y + misize.y );
        
        float2 distances = max( ZERO, abs( rectCenter - outCoord() ) - misize );
        
        float rdist = length( distances );
        
       // float2 rdist = ( float2( distance( rectCenter.x, outCoord().x ), distance( rectCenter.y, outCoord().y ) ) - misize ) / dist ;
        
        dst = mix( insetColor, outsetColor, rdist );
    }
}
