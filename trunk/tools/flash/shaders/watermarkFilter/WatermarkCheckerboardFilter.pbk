<languageVersion : 1.0;>

kernel WatermarkOneOneFilter
<   namespace : "fr.digitas.flowearth.shaders::WatermarkOneOneFilter";
    vendor : "Pierre Lepers";
    version : 1;
    description : "Add a watermark with a 'checkerboard' like pattern.";
>
{
    
    parameter float4 color
    < 
        description : "watermark color";
        defaultValue:float4( 0.0, 0.0, 0.0, 1.0 ); 
    >;
    
    parameter float2 size
    < 
        description : "tiles size";
        defaultValue:float2( 2.0, 2.0 ); 
    >;
    
    
    input image4 src;
    
    output pixel4 dst;

    void
    evaluatePixel()
    {
        bool2 mods = greaterThan( mod(  outCoord(), size ) , size/2.0 );
        if( mods.x ^^ mods.y ) {
            dst = color;
            dst.a *= sample( src, outCoord() ).a;
        } else {
            dst = sample( src,outCoord() );
        }
    }
}
